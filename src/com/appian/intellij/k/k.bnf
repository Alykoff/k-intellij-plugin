{
  parserClass="com.appian.intellij.k.parser.KParser"
  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="K"
  psiImplClassSuffix="Impl"
  psiPackage="com.appian.intellij.k.psi"
  psiImplPackage="com.appian.intellij.k.psi.impl"

  elementTypeHolderClass="com.appian.intellij.k.psi.KTypes"
  elementTypeClass="com.appian.intellij.k.psi.KElementType"
  tokenTypeClass="com.appian.intellij.k.psi.KTokenType"

  /*  http://kparc.com/parse.txt

/``"."
/("\\mkdir -p \"";"\\mkdir \"")
/`$r
/rf:|f$:

*-1#x

  TOKEN ;a0`"+

  a:[a-zA-Z]                         / alpha
  d:[0-9]                            / digit
  p:[;()[]{}]                        / punctuation
  o:[:+-*%!&|<>=~,^#_$?@.'/\]        / operator

  n:(![]})ad]-)?(d*.)?d+(e-?d+)?        / number, e.g. -2
  i:a[ad]*                              / identifier
  s:`[ad]*                              / symbol
  t:p|i(.i)*|n( n)*|s+|"_*"|o:?| (/_*)? / token

  (+:;a.b;2 -.3;`a`bc) / nine tokens and comment

  lexical analysis is fairly simple. punctuation is normal. operators have optional':'.
  characters("ab"), numbers(2 -3) and symbols(`a`bc) are atom or vector(more than one).
  "2-3" is three tokens and "2 -3" is one token. end-of-line comment with [space]/ .

  PARSE Noun(x) Verb(+) Adverb(/)

  e:nve|te| t:n|v v:tA|V n:t[E]|(E)|[E]|{E}|N E:;e

  (+/x)%#x  is  (%;((/;+);x);(#:;x))

  SCRIPTS have nested multi-line comments with / and \ .
  multi-line functions(and data) with indentation. newlines are treated like ;'s .  */

  tokens=[

    // ascii chars
    bang='!'
    quote='"'
    hash='#'
    dollar='$'
    percent='%'
    ampersand='&'
    open_paren='('
    close_paren=')'
    asterisk='*'
    plus='+'
    comma=','
    dash='-'
    dot='.'
    colon=':'
    semicolon=';'
    less_than='<'
    equals='='
    greater_than='>'
    question_mark='?'
    at='@'
    open_bracket='['
    close_bracket=']'
    caret='^'
    underscore='_'
    back_tick='`'
    open_brace='{'
    pipe='|'
    close_brace='}'
    tilde='~'

    // adverbs
    slash='/'
    slash_colon='/:'
    back_slash='\'
    back_slash_colon='\:'
    tick="'"
    tick_colon="':"

    // names
    identifier='regexp:[a-zA-Z][._a-zA-Z0-9]*'
    identifier_sys='regexp:_[._a-zA-Z0-9]*|[0-6]:'

    // nouns
    number='regexp:((0|[1-9]\d*)(\.\d+)?([eE][+-]?\d*)?|0[iInN])'
    char='regexp:"(\\"|[^"])"'
    string='regexp:"(\\"|[^"])*"'

    // system verbs and nouns

    // i/o, dynamic load and client/server
    zeroColon='0:'
    oneColon='1:'
    twoColon='2:'
    threeColon='3:'
    fourColon='4:'
    fiveColon='5:'
    sixColon='6:'

    // assign, define, control and debug
    if="if"
    do="do"
    while="while"

    // punctuation
    newline='regexp:\r|\n|\r\n'
  ]
}

root ::= E0+ <<eof>>
private E0 ::= directory
             | E1
private E1 ::= separator
             | E4
private E2 ::= separator
             | E3
private E3 ::= E4
             | verb+
             | composed_monad
private E4 ::= control
             | comment
             | E5
private E5 ::= value
             | result
private E6 ::= E5
             | verb+
             | composed_monad

private separator ::= semicolon | newline

group ::= open_paren E3 close_paren

private id ::= identifier | identifier_sys

private value ::= dyad | assignment | monad_value
dyad ::= noun infix value
private monad_value ::= monad_or_projection | noun
private monad_or_projection ::= monad | projection
monad ::= monad_op value
private monad_op ::= verb | derived_verb | noun_and_op

private noun ::= noun_only | noun_and_op
private noun_only ::= vector | atom
private noun_and_op ::= trap | conditional | indexing | group | id | lambda | list
//private compound_expr ::= lambda | indexing | list

private vector ::= symbol_vector | number_vector | string
list ::= open_paren ([E3] separator [E3])* close_paren

private atom ::= symbol | number | char
lambda ::= open_brace [index] E1* close_brace

indexing ::= (verb | dot | at | trap | conditional | group | id | lambda | list) index+
//indexing ::= (verb | noun_and_op) index
index ::= open_bracket E2* close_bracket
control ::= (if | do | while) index

private infix ::= verb | identifier_sys | derived_verb

projection ::= noun infix !noun

private assignment ::= global_assignment | local_assignment
global_assignment ::= id colon colon E5
local_assignment ::= id [index] (colon | composed_monad) E5

conditional ::= colon index

result ::= colon E5

private verb_or_colon ::= verb | colon

trap ::= (dot | at) open_bracket E6 separator E3 separator colon close_bracket
